
;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main Orders

    .include "WND:windows.def"
    ;.include "INC:header.def"
	;.include "INC:detail.def"
	.include "HEADER" repository, record="header"
	.include "DETAIL" repository, structure ="strDetail"

    ;record
     ;   detail,		strDetail
    ;endrecord

    record datetime
        year,		d4
        month,		d2 
        day,		d2
        hour,		d2
        minute,		d2
        second,		d2
    endrecord
		
    record date
        ,		d8
    endrecord

    record 
        term_chnl,		i4, 1
        input_chnl,		a1
        new_menu,		i4
		input_key,		a1
		order_wind,     i4 
        detail_wind,    i4
	endrecord

	record
		mh,             D_HANDLE        ;handle
		ms,             i4              ;size
        mc,             i4              ;counter
	endrecord

	record
		chnl1,          i4
		chnl2,          i4
		ix,				i4
	endrecord

proc

    datetime = %datetime
    date = datetime 

    call startup	
    call process
    call shutdown

    stop

startup, 
    ;terminal screen 
    open (term_chnl, o, "tt:")
    xcall flags(7004020,1)
    display(term_chnl, $SCR_CLR(SCREEN))

    ;window
    xcall w_init(1, term_chnl, 5)
	xcall w_caption(WC_SET, "My First Synergy Application")

	;dynamic memory
	mh = %mem_proc(DM_ALLOC, ^size(strDetail)*(ms = 3))

	repeat
	begin
		open(chnl1, U:I, "DAT:header.ism") [$ERR_FNF = hnf]
        exitloop
	hnf,
		xcall isamc("DAT:header.ism", ^size(header),1, "start = 1, length = 6, nodups, nomodify") 
	end

	repeat
	begin
		open(chnl2, U:I, "DAT:detail.ism") [$ERR_FNF = dnf]
		exitloop
	dnf,
		xcall isamc("DAT:datail.ism", ^size(strDetail),1, "start = 1:7, length = 6:4, nodups, nomodify") 
	end

    return 

process,
    ;terminal screen 
    ;display(term_chnl,	$scr_pos(8,10), "Welcome",
    ;&					$scr_pos(10,10), datetime,
    ;&					$scr_pos(12,15), "OK ?")
    ;accept(term_chnl, input_chnl)

    ;window
    ;xcall display_message("Bonjour")
    ;xcall display_message("Bonjour, appuyer sur une touche")

    xcall create_window(new_menu, "Menu window", 10, 50, 2, 20, "Order Processing Menu") 

    xcall display_text(new_menu, 3, 2, "[A] Add an order")
    xcall display_text(new_menu, 4, 2, "[V] View latest order")
    xcall display_text(new_menu, 5, 2, "[E] Exit")

    xcall display_text(new_menu, 7, 2, "Select an option")

    repeat
    begin
        xcall wait_key(new_menu, 7, 20, input_key)

        using input_key select
        ("A", "a"),
            call order_header
        ("V","v"),
            call view_order
        ("E","e"),
            exitloop
        (),
            xcall display_message("Error : invalid option")
        endusing
    end 

    xcall delete_window(new_menu)

    return

shutdown,
    ;window
    xcall w_exit

    ;terminal screen 
    display(term_chnl, $SCR_CLR(SCREEN))
	close term_chnl

    ;dynamic memory
	mh = %mem_proc(DM_FREE, mh)

    close chnl1, chnl2 

	return

order_header,

	xcall create_window(order_wind, "Order", 10, 50, 5, 20, "Order Header") 

	xcall display_text(order_wind, 3, 2, "Order number          :")
	xcall display_text(order_wind, 4, 2, "Customer account code :")
	xcall display_text(order_wind, 5, 2, "Order date            :")
	xcall display_text(order_wind, 6, 2, "Order placed by       :")
	xcall display_text(order_wind, 7, 2, "Sales rep code        :")

	repeat
	begin
		xcall input(order_wind, 3, 30, header.oh_number)

		if (!header.oh_number)
            exitloop
		;check if an order already exists
		if (%file_access(FileOp.Find, chnl1,,header.oh_number) == FileResult.OK)
		begin
			xcall display_message("Order already exists")
            nextloop
		end

		xcall input(order_wind, 4, 30, header.oh_customer, true)
		xcall input(order_wind, 5, 30, header.oh_date, , , true)
		xcall input(order_wind, 6, 30, header.oh_contact, true)
		xcall input(order_wind, 7, 30, header.oh_salesrep, true)

		call order_detail

		if(mc)
		begin
			if(%file_access(FileOp.Create, chnl1, header) != FileResult.OK) then
				xcall display_message("Failed : Header record not saved")
			else
				for ix from 1 thru mc
					if(%file_access(FileOp.Create, chnl2, ^m(strDetail[ix],mh)) != FileResult.OK)
				begin
				xcall display_message("Failed to an order detail record")
				exitloop
				end
		end
        xcall clear_box(order_wind, 3, 30, 7, 50)
	end 

	xcall delete_window(order_wind)

	return
    
order_detail,

    mc = 0

	xcall create_window(detail_wind, "Detail", 10, 50, 5, 20, "Order Detail") 

	xcall display_text(detail_wind, 3, 2, "Order number      :" + %string(header.oh_number, "XXXXXX"))
	xcall display_text(detail_wind, 4, 2, "Item number       :")
	xcall display_text(detail_wind, 5, 2, "Part number       :")
	xcall display_text(detail_wind, 6, 2, "Quantity ordered  :")
	xcall display_text(detail_wind, 7, 2, "Unit price        :")

	repeat
	begin
		mc = mc + 1
		if (mc > ms)
            mh = %mem_proc(DM_RESIZ, ^size(strDetail)*(ms = ms + 3), mh)

		^m(strDetail[mc].od_number, mh) = header.oh_number
		^m(strDetail[mc].od_item, mh) = mc

		xcall display_text(detail_wind, 4, 25,%string(mc))

		xcall input(detail_wind, 5, 25, ^m(strDetail[mc].od_part, mh))

		if (!^m(strDetail[mc].od_part, mh))
		begin
            mc = mc - 1
			exitloop
		end 

		xcall input(detail_wind, 6, 25, ^m(strDetail[mc].od_qty, mh), true)
		xcall input(detail_wind, 7, 25, ^m(strDetail[mc].od_unit_price, mh), true)

		xcall clear_box(detail_wind, 5, 25, 7, 50)
	end 

	xcall delete_window(detail_wind)

    return

view_order,

	xcall create_window(order_wind, "Order", 10, 50, 5, 20, "View Order Header") 

	xcall display_text(order_wind, 3, 2, "Order number          :")
	xcall display_text(order_wind, 4, 2, "Customer account code :")
	xcall display_text(order_wind, 5, 2, "Order date            :")
	xcall display_text(order_wind, 6, 2, "Order placed by       :")
	xcall display_text(order_wind, 7, 2, "Sales rep code        :")

	xcall display_text(order_wind, 9, 2, "Press a key			:")

	repeat
	begin
		xcall input(order_wind, 3, 30, header.oh_number)

		if (!header.oh_number)
			exitloop
		;check if an order already exists
		if (%file_access(FileOp.Read, chnl1,header,header.oh_number) != FileResult.OK)
		begin
			xcall display_message("Order does not exist")
			nextloop
		end

		xcall display_text(order_wind, 4, 30, header.oh_customer)
		xcall display_text(order_wind, 5, 30, %string(header.oh_date, "XXXX/XX/XX"))
		xcall display_text(order_wind, 6, 30, header.oh_contact)
		xcall display_text(order_wind, 7, 30, %string(header.oh_salesrep))

		xcall wait_key(order_wind, 9, 30, input_key) 

		call view_order_details

		xcall clear_box(order_wind, 3, 30, 7, 50)
	end 

	xcall delete_window(order_wind)
	return

view_order_details,

	mc = 0

	xcall create_window(detail_wind, "Detail", 10, 50, 5, 20, "View Order Detail") 

	xcall display_text(detail_wind, 3, 2, "Order number      :" + %string(header.oh_number, "XXXXXX"))
	xcall display_text(detail_wind, 4, 2, "Item number       :")
	xcall display_text(detail_wind, 5, 2, "Part number       :")
	xcall display_text(detail_wind, 6, 2, "Quantity ordered  :")
	xcall display_text(detail_wind, 7, 2, "Unit price        :")

	xcall display_text(detail_wind, 9, 2, "Press a key       :")
	
	if(%file_access(FileOp.Find, chnl2,,header.oh_number) != FileResult.OK)
		nop

	repeat
	begin
		mc = mc + 1

		if((%file_access(FileOp.ReadNext, chnl2,, ^m(strDetail[mc], mh)) != FileResult.OK)
		&		|| (^m(strDetail[mc].od_number,mh) != header.oh_number))
			exitloop

		xcall display_text(detail_wind, 4, 25,%string(mc))

		xcall display_text(detail_wind, 5, 25, ^m(strDetail[mc].od_part, mh))

		xcall display_text(detail_wind, 6, 25, %string(^m(strDetail[mc].od_qty, mh)))
		xcall display_text(detail_wind, 7, 25, %string(^m(strDetail[mc].od_unit_price, mh)))

		xcall wait_key(detail_wind, 9, 25, input_key) 

		xcall clear_box(detail_wind, 5, 25, 7, 50)
	end 

	xcall delete_window(detail_wind)

	return
endmain